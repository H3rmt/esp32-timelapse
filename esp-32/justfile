call_recipe := just_executable() + " --justfile=\"" + justfile() + "\""

build env="wired":
    platformio run -e {{ env }}

upload-wired port: (build "wired")
    #!/usr/bin/env bash
    platformio run -e wired -t upload --upload-port {{ port }}

upload-wireless:
    #!/usr/bin/env bash
    [ -f .env ] && source .env
    export PLATFORMIO_UPLOAD_FLAGS="--auth='${OTA_PASSWORD}'"
    platformio run -e ota -t upload --upload-port ${ESP_HOSTNAME}.local

upload:
    #!/usr/bin/env bash
    device=$(ls /dev/ttyUSB* /dev/ttyACM* 2>/dev/null | head -n1) || true;
    if [ -n "$device" ]; then
      echo "Uploading via wired connection to $device"
      {{ call_recipe }} upload-wired "$device"
    else
      echo "No wired device found, uploading via wireless"
      {{ call_recipe }} upload-wireless
    fi

monitor device="auto":
    #!/usr/bin/env bash
    if [ "{{ device }}" = "auto" ]; then
      device=$(ls /dev/ttyUSB* /dev/ttyACM* 2>/dev/null | head -n1);
      [ -n "$device" ] || { echo "No serial device found"; exit 1; };
    else
      device="{{ device }}";
    fi;
    echo "Opening serial monitor on $device"
    exec minicom -D "$device" -b 115200

clean:
    platformio run -t clean